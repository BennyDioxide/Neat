buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${project.version}-${project.build_number}"
group = "vazkii.${project.mod_id}"
archivesBaseName = project.mod_name

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

compileJava.options.compilerArgs << "-Xlint:all,-classfile,-processing,-serial" << "-Werror"

minecraft {
    mappings channel: "official", version: "${project.mc_version}"
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            mods {
                neat {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            mods {
                neat {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.mc_version}-${project.forge_version}"
}

processResources {
    filesMatching('META-INF/mods.toml') {
        // replace version and mcversion
        expand 'version': project.version
    }
}

jar {
     manifest {
        attributes([
            "Specification-Title": "${project.mod_id}",
            "Specification-Vendor": "vazkii",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": "${project.mod_id}",
            "Implementation-Version": "${project.version}",
            "Implementation-Vendor" :"vazkii",
        ])
    }
}

defaultTasks 'clean', 'build'
